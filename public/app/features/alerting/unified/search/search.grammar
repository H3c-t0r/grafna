@top AlertRuleSearch { expression+ }

expression { (FilterExpression | FreeFormExpression) expression }

FilterExpression {
 DataSourceFilter |
 NameSpaceFilter |
 LabelFilter |
 GroupFilter |
 RuleFilter |
 StateFilter |
 TypeFilter
}

DataSourceFilter { filter<"ds"> }
NameSpaceFilter { filter<"ns"> }
LabelFilter { filter<"label"> }
GroupFilter { filter<"group"> }
RuleFilter { filter<"rule"> }
StateFilter { filter<"state"> }
TypeFilter { filter<"type"> }

filter<type> { filterToken<type> FilterValue }

FreeFormExpression { word (colon word)* }

@tokens {
  colon { ":" }

  // Special characters (except colon, quotes and space), Latin characters, extended latin and emoji
  allowedInputChar { $[!#$%&'()*+,-./] | $[\u{0030}-\u{0039}] | $[\u{003b}-\u{1eff}] | $[\u{2030}-\u{1faff}] }
  word { allowedInputChar+ }

  filterNoBracketChars { allowedInputChar | ":" }
  filterWithBracketChars { filterNoBracketChars | " " }

  filterToken<type> { type colon }
  FilterValue { filterNoBracketChars+ | ("\"" filterWithBracketChars+ "\"") }

  @precedence { filterToken, word }
}
